import org.gradle.api.JavaVersion

apply plugin: 'java'
apply plugin: 'maven'

ext {
    vendor = [name: 'Dale H. Emery', group: 'com.dhemery']
}

project.description = 'Impulse Control'
project.group = vendor.group
project.version = "1.0.0"

tasks.install.dependsOn clean

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

tasks.jar {
    manifest {
        attributes(
                "Implementation-Title": project.description,
                "Implementation-Version": project.version,
                "Implementation-Vendor": vendor.name
        )
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://maven.bitwig.com"
    }
}

dependencies {
    compile "com.bitwig:extension-api:3+"
}

task wrapper(type: Wrapper) {
    gradleVersion = "3.5"
}

def bitwigExtensionsDirectory = System.getProperty("user.home") + '/Documents/Bitwig Studio/Extensions'

task bitwig(type: Copy, dependsOn: jar) {
    from jar.archivePath
    into bitwigExtensionsDirectory
    rename { String fileName ->
        fileName.replace('.jar', '.bwextension')
    }
}


defaultTasks 'clean', 'bitwig'